Emoji {
      Functionality = Functionality+ | Input | Output | Condition | Variable | Loop | Single_Emoji
      Single_Emoji = Input_Emoji | Output_Emoji | Variable_Emoji | Condition_Identifier_Emoji | Condition_True_Emoji | Condition_False_Emoji | Compare_Operator_Emoji | Loop_Emoji | Random_Emoji
  
      /*
      * INPUT
      */
      Input 
      = Input_Emoji+ Arrow_Right Variable_Emoji                                                                -- No_Type
      | (Input_Emoji (Type_Numeric_Emoji | Type_String_Emoji) Type_Required_Emoji ?)+ Arrow_Right Variable_Emoji -- Type_Required
      
      Input_Emoji = ":memo:" | "📝" | ":microphone:" | "🎤" | ":microphone_studio:" | "🎙️" | ":camera_with_flash:" | "📸" | ":camera:" | "📷" | ":framed_picture:" | "🖼️"
  
      /*
      * OUTPUT
      */
      Output
      = Variable_Emoji Arrow_Right Output_Emoji -- From_Variable
      | Output_Emoji String  -- From_String
      
      Output_Emoji = ":speech:" | "💬" | ":loud_sound:" | "🔊" | ":megaphone:" | "📢" | ":robot:" | "🤖" | ":satellite:" | "📡"
      
      /*
      * VARIABLE
      */
      Variable 
      = Variable_Emoji Arrow_Left Input_Emoji+        -- Normal
      // "Random_String" bislang deaktiviert
      | Random_Number Arrow_Right Variable_Emoji      -- Right_Side_Add_Random_Value
      | Variable_Emoji Arrow_Left Random_Number       -- Left_Side_Add_Random_Value
      | Variable_Emoji                                -- Empty
  
  
      Variable_Emoji = ":cardfilebox:" | "🗃️"
      
      /*
      * CONDITION
      */
      Condition = Compare_Value Compare_Operator_Emoji Compare_Value Condition_Identifier_Emoji (Condition_Flow | end)
      Condition_Flow
      = (Condition_True_Emoji Functionality Condition_False_Emoji Functionality | Condition_False_Emoji Functionality Condition_True_Emoji Functionality)  -- Double_Path
      | (Condition_True_Emoji | Condition_False_Emoji) Functionality                                      -- Single_Path
      | (Condition_True_Emoji | Condition_False_Emoji)                                      -- Empty_Path
  
      Condition_Identifier_Emoji = ":question:" | "❓"
      Condition_True_Emoji = ":thumbs_up:" | "👍"
      Condition_False_Emoji = ":thumbs_down:" | "👎"
  
      Compare_Operator_Emoji 
      = ":balance_scale:" | "⚖️" 
      | ":equals:" | "="
      |":chart_with_downwards_trend:"
      | "📉" | ":chart_with_upwards_trend:"
      | "📈"
      
      // Begriffe in [] beziehen sich auf Variablen
      // Wird ggf. separiert werden müssen
      Compare_Value = Variable_Emoji | Compare_Value_Emoji
  
      Compare_Value_Emoji
      = "[NOT_EMPTY]" | "[EMPTY]"
      | "[FIRST]" | "[MIDDLE]" | "[LAST]"
  
      /*
      * LOOP
      */
      Loop
      = Loop_Emoji Integer Functionality          -- Timed_Loop
      | Loop_Emoji Condition Condition_Flow       -- Condition_Loop
      | Loop_Emoji (Integer | Condition)          -- Empty_Loop
  
      Loop_Emoji = ":repeat:" | "🔁"
       
      /*
      * HELPERS
      */
      Arrow_Right = ":arrow_right:" | "➡️"
      Arrow_Left = ":arrow_left:" | "⬅️"
  
      Random_Number = Random_Emoji Type_Numeric_Emoji Type_Numeric_Emoji ?
      // Wie würde "Random_String" aussehen? Was würde passieren? 
      Random_String = Random_Emoji Type_String_Emoji
      Random_Emoji = ":twisted_rightwards_arrows:" | "🔀"
  
      /*
      * TYPES
      */
      Type_Required_Emoji = ":exclamation:" | "❗"
  
      Type_Numeric_Emoji = ":1234:" | "🔢"
      Type_String_Emoji = ":abc:" | "🔤"
  
      String
      = "\"" (alnum | specialCharacters)* "\"" -- Double_Quoutes
      | (alnum | specialCharacters)* -- No_Double_Quoutes
  
      Integer = number+
      
      Letters = letter+
      specialCharacters = "!" | "?" | "<" | ">" | "§" | "$" | "&" | "/" | "," | ";" | "." | ":" | "_" | "#" | "'" | "~" | "|" | "^" | "′"
      
      ident  (an identifier)
      = letter alnum*
      
      number  (a number)
      = digit* "." digit+  -- fract
      | digit+             -- whole
  }